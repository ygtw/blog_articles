我好像忘記提 system call 的實作, 雖然我在 simple_os 已經實作過 system call, 不過那時候的程式碼是從書上抄來, 我打算在 arm cortex m3 重新實作一次, 果然遇到不少麻煩, 別人的東西就是別人的, 沒有親身體會這段, 就沒有烙印在自己身上的記憶。現在來補上這塊拼圖。

system call 會涉及到 context switch, 所以需要先理解 context switch (可不是教科書說的理論那種理解, 而是能寫出 context switch code 的那種理解) 才能看懂這裡提到的程式碼。 

<b>麻煩一</b>
在使用 svc 來實作 system call, 還是要保存 context, 也就是 context switch 要做的事情, 都要再做一次, 怎麼讓他們共用這段程式碼呢？

<b>麻煩二</b>
如何將 system call 的結果傳回給 proc_a (proc_a 是發動這個 system call 的 process)

<b>麻煩三</b>
如何從 proc_a 傳參數到 system call 裡頭呢？

這個範例程式很醜, 架構也不美, 但它可是我從無到有實實在在完成的, 對我來說, 意義非凡。

本來打算使用 <a href="http://descent-incoming.blogspot.tw/2011/12/oranges.html">Orange's 一個作業系統的實現</a> 上的實作方式, 我也想好怎麼開始了, 不過 ...

先來看看 <b>Orange's 一個作業系統的實現</b>是怎麼<strike>實現</strike>實作 system call:

程式碼請參閱 <a href="https://github.com/descent/simple_os" target="_blank">simple_os</a>

asm_syscall.S
<pre style="font-family: serif; white-space: pre-wrap;">.global get_ticks
get_ticks:
  mov $_NR_GET_TICKS, %eax
  int $INT_VECTOR_SYS_CALL
  ret

.global write
write:
  mov $_NR_WRITE, %eax
  mov 4(%esp), %ebx
  mov 8(%esp), %ecx
  int $INT_VECTOR_SYS_CALL
  ret

.global sys_call
sys_call:
  call save
  sti 
  push %esi # why push %esi ??

  pushl ready_process
  push %edx
  push %ecx
  push %ebx
  call *sys_call_table(, %eax, 4)
  add $16, %esp
  pop %esi # why pop %esi ??
  mov %eax, P_EAX_OFFSET(%esi) # return value save to eax offset in process structure
  cli
  ret
</pre>

填入 system call 編號到 %eax, 觸發 0x90 中斷 (int 0x90), 這時候會跑到 sys_call (透過 IDT 設定的結果), 再根據 sys_call_table, 編號去執行 sys_call_table 裡頭的 function (ref: syscall.c), 再把傳回值寫到 process stack frame 的 %eax 欄位, system call 結束後, process 的 %eax 自然就得到該 system call 的傳回值。

如何把參數傳給 system call:
以 write 為例子, 將傳進來的參數存到 %ebx, %ecx, 然後在執行 sys_call_table 裡頭的 function 之前 push 到 stack, 請參閱 sys_call。

文字敘述很簡單 (所以通常被稱做嘴炮), 把它變成程式碼就困難些了。 

不過後來找到這篇的實作方法:
<a href="http://www.coactionos.com/embedded-design/133-effective-use-of-arm-cortex-m3-svcall.html" target="_blank">Effective Use of ARM Cortex-M3 SVCall</a>
我想試試看這個作法。

好了, 該輪到 cm3 的<strike>實現</strike>實作了, 一樣拿 get_tics() 來開刀, 這是蠻適合的一個例子, 在 cm3 上, 就是把 systick_isr 累加的 ticks 回傳給<strike>調用</strike>呼叫 (我不厭其煩地加上這些刪除線, 希望有人能理解我的苦心) 的那個 process。

<fieldset>
<legend>asm_func.S</legend>
<pre style="white-space: pre-wrap;"><b>  1</b> .text
<b>  2</b> .global _start
<b>  3</b> .code 16
<b>  4</b> .syntax unified
<b>  5</b> 
<b>  6</b> @void run(char* sp);
<b>  7</b> @ sp put in r0
<b>  8</b> .global run
<b>  9</b> run:
<b> 10</b>   ldr     r4, [r0, #(4 * 14)]     @ Load process entry point into R4
<b> 11</b>   add     r0, #(4 * 16)           @ emulate context restore
<b> 12</b>   mov sp, r0
<b> 13</b> 
<b> 14</b>   bl asm_init_systick
<b> 15</b> 
<b> 16</b>   cpsie i @ Enable interrupts at processor level
<b> 17</b>   bx r4 
<b> 18</b> 
<b> 19</b> 
<b> 20</b> @ void asm_init_systick();
<b> 21</b> .global asm_init_systick
<b> 22</b> asm_init_systick:
<b> 23</b> @ systick setting
<b> 24</b> @ 0xe000ed24 check systick isr active
<b> 25</b> @ 0xe000e018 check systick count
<b> 26</b> @ ref: arm cortex-m3: 嵌入式系統設計入門 p8-12
<b> 27</b> 
<b> 28</b>   ldr r0, =0xe000e010 @ SysTick Ctrl &amp; Status Reg
<b> 29</b>   mov r1, #0
<b> 30</b>   str r1, [r0]
<b> 31</b>   ldr r1, =0x3fff
<b> 32</b>   str r1, [r0, #4]
<b> 33</b>   str r1, [r0, #8]
<b> 34</b>   mov r1, #0x7
<b> 35</b>   str r1, [r0] @ enable systick
<b> 36</b>   bx lr
<b> 37</b> 
<b> 38</b> #if 1
<span style="color: blue;"><b> 39</b> .type svc_isr, function
<b> 40</b> .global svc_isr
<b> 41</b> svc_isr:
<b> 42</b>   cpsid i @Prevent interruption during svc
<b> 43</b>   push {r4-r11}
<b> 44</b>   @mov r12, r0
<b> 45</b>   mov r0, sp
<b> 46</b> 
<b> 47</b>   ldr r3, =kernel_stack
<b> 48</b>   mov sp, r3
<b> 49</b>   push {lr}
<b> 50</b> 
<b> 51</b>   bl save_cur_proc_stack
<b> 52</b>   
<b> 53</b> 
<b> 54</b>   @ run sys_call_func
<b> 55</b>   @ get svc number
<b> 56</b>   @ldr r1, =sys_call_table
<b> 57</b> 
<b> 58</b>   @ get process stack frame pointer to r0
<b> 59</b>   bl restore_cur_proc_stack
<b> 60</b>   push {r0}
<b> 61</b> 
<b> 62</b>   ldr r12, [r0, #32]
<b> 63</b>   ldr r0, [r0, #36]
<b> 64</b>   
<b> 65</b>   blx r12 @ call system call, parameter address is in r0
<b> 66</b>   @ write r0 to ready stack frame r0
<b> 67</b>   pop {r2}
<b> 68</b>   str r0, [r2, #32]
<b> 69</b> 
<b> 70</b> 
<b> 71</b>   bl restore_cur_proc_stack
<b> 72</b> 
<b> 73</b>   pop {lr}
<b> 74</b> 
<b> 75</b>   mov sp, r0
<b> 76</b>   pop {R4-R11}     // Restore r4-11 from new process stack
<b> 77</b> 
<b> 78</b>   cpsie i
<b> 79</b>   bx lr</span>
<b> 80</b> #endif
<b> 81</b> 
<b> 82</b> @void pendsv_isr(void)
<b> 83</b> .type pendsv_isr, function
<b> 84</b> .global pendsv_isr
<b> 85</b> pendsv_isr:
<b> 86</b>   cpsid i @Prevent interruption during context switch
<b> 87</b>   push {r4-r11}
<b> 88</b>   mov r0, sp
<b> 89</b> 
<b> 90</b>   ldr r1, =kernel_stack
<b> 91</b>   mov sp, r1
<b> 92</b>   push {lr}
<b> 93</b>   bl schedule
<b> 94</b>   pop {lr}
<b> 95</b> 
<b> 96</b>   mov sp, r0
<b> 97</b>   pop {R4-R11}     // Restore r4-11 from new process stack
<b> 98</b> 
<b> 99</b>   cpsie i
<b>100</b>   bx lr
<b>101</b> 
<b>102</b> 
<b>103</b> 
<b>104</b> .section .stackares,"aw",%progbits
<b>105</b> .space  0x200, 0
<b>106</b> kernel_stack:
</pre>
</fieldset>

<fieldset>
<legend>syscall.cpp</legend>
<pre style="white-space: pre-wrap;"><b> 1</b> #include "asm_syscall.h"
<b> 2</b> 
<b> 3</b> typedef void* SystemCall;
<b> 4</b> 
<b> 5</b> struct Arg
<b> 6</b> {
<b> 7</b>   int ret;
<b> 8</b> };
<b> 9</b> 
<b>10</b> typedef void (*FuncPtr)(Arg *);
<b>11</b> 
<b>12</b> void sys_get_ticks(Arg *arg)
<b>13</b> {
<b>14</b>   extern int ticks;
<b>15</b> 
<b>16</b>   arg-&gt;ret = ticks;
<b>17</b>   //return ticks;
<b>18</b> }
<b>19</b> 
<b>20</b> // ref: http://www.coactionos.com/embedded-design/133-effective-use-of-arm-cortex-m3-svcall.html
<b>21</b> int service_call(FuncPtr func_ptr, Arg *arg)
<b>22</b> {
<b>23</b>   __asm__ volatile("svc 0");
<b>24</b> }
<b>25</b> 
<b>26</b> int get_ticks()
<b>27</b> {
<b>28</b>   Arg arg;
<b>29</b> 
<b>30</b>   service_call(sys_get_ticks, &amp;arg);
<b>31</b>   return arg.ret;
<b>32</b> }
<b>33</b> 
<b>34</b> SystemCall sys_call_table[NR_SYS_CALL] = {(void*)sys_get_ticks};
</pre>
</fieldset>

asm_func.S L39 ~ 79 就是在處理 system call 的部份, 有些說明請參考以下圖片, 呃 ... 我也不願意這麼做, 實在是太沒質感了, 應該畫個漂亮的圖才是, 有人願意幫忙嗎? 裡頭涉及到 context switch, 會有點複雜。

<center>
<a href="https://picasaweb.google.com/lh/photo/TaEPtNvc0rqYVZ7gM-r_rNMTjNZETYmyPJy0liipFm0?feat=embedwebsite"><img height="600" src="https://lh4.googleusercontent.com/-vEQ1YM-wfI4/UlIw0-KaofI/AAAAAAAAs_s/M5rUC6JRIfA/s800/P1350042.JPG" width="800" /></a>
</center>

get_ticks() 是 system call, 將 {sys_get_ticks(), arg} 當成參數傳給 service_call(), service_call() 會執行 svc 觸發 svc exception, 再來便是上圖所敘述從 svc_isr 開始到返回。

這邊需要知道 cm3 c function 和組合語言如何傳遞參數, r0~r3 用來傳遞參數, 如果有第五個參數, 就要用 stack 來傳遞, 返回值則存在 r0。這裡同樣會有 context 的切換, 所以如果沒搞懂 context switch 如何實作, 這邊應該也是看不懂, 金字塔知識就是這樣堆積起來的。

程式若用上兩個 stack 會簡單點, 但這麼<b>直白</b>的程式, 比較好理解整個來龍去脈, 裡頭有著 user process, kernel code 的 stack 切換, 可以了解到一個 system call 的複雜, 和一般的 function call 可是有本質上的不同。

三個麻煩順利解決 (你說<b>麻煩一</b>沒解決是吧, 管他的, 做出來最重要, 美感的話再說了), 痛快。

system call 和 function call 有什麼不同呢? Expert C Programming a.4 提到了這個面試題目, 還很好心地給了答案:

<table border="1"><caption> Library Call versus System Call </caption> <tbody>
<tr> <td>
Library Call </td> <td>
System Call</td> </tr>
<tr> <td>
The C library is the same on every ANSI C implementation</td> <td>
The systems calls are different in each OS</td> </tr>
<tr> <td>
Is a call to a routine in a library </td> <td>
Is a call to the kernel for a service</td> </tr>
<tr> <td>
Linked with the user program 

</td> <td>
Is an entry point to the OS</td> </tr>
<tr> <td>
Executes in the user address space </td> <td>
Executes in the kernel address space</td> </tr>
<tr> <td>
Counts as part of the "user" time </td> <td>
Counts as part of the "system" time</td> </tr>
<tr> <td>
Has the lower overhead of a procedure call </td> <td>
Has high overhead context switch to kernel and
back</td> </tr>
<tr> <td>
There are about 300 routines in the C library libc </td> <td>
There are about 90 system calls in UNIX, (fewer in MS-DOS)</td> </tr>
<tr> <td>
Documented in Section 3 of the UNIX OS
manual

</td> <td>
Documented in Section 2 of the UNIX OS manual</td> </tr>
<tr> <td>
Typical C library calls: system, fprintf, malloc 

</td> <td>
Typical system calls: chdir, fork, write, brk</td> </tr>
</tbody></table>

